= 类与对象
:toc-title: 目录
:description: 在 Perl6 中创建使用类的指南
:keywords: perl6, perl 6, perl 6 document
:Email: blackcatoverwall@gmail.com
:Revision: 1.0
:icons: font
:source-highlighter: pygments
:source-language: perl6
:pygments-linenums-mode: table
:toc: left
:doctype: book
:lang: zh

`Perl6` 有丰富的内置语法用来定义以及使用类。

默认构造函数允许你在创建对象时设置属性的值：

[source,perl6]
----------------
class Point {
    has Int $.x;
    has Int $.y;
}
 
class Rectangle {
    has Point $.lower;
    has Point $.upper;
 
    method area() returns Int {
        ($!upper.x - $!lower.x) * ( $!upper.y - $!lower.y);
    }
}
 
# 从两个 Points 创建一个新的 Rectangle
my $r = Rectangle.new(lower => Point.new(x => 0, y => 0), upper => Point.new(x => 10, y => 10));
 
say $r.area(); # 输出： «100␤» 
----------------
你也可以提供自己的构造函数以及构建（`build`）实现。
以下代码更详细的展示了依赖关系处理在 `Perl6` 中的模样，它含有自定义构造函数（`constructors`）、
私有和公有属性（`attribute`）、方法（`method`）以及各种样式的签名（`signature`）。
廖廖几行代码，但是结果很有意思。

[source,perl6]
------------------------------
class Task {
    has      &!callback;
    has Task @!dependencies;
    has Bool $.done;
 
    # 通常不需要写
    # BUILD 等价与其他语言中的构造函数
    method new(&callback, *@dependencies) {
        return self.bless(:&callback, :@dependencies);
    }
 
    submethod BUILD(:&!callback, :@!dependencies) { }
 
    method add-dependency(Task $dependency) {
        push @!dependencies, $dependency;
    }
 
    method perform() {
        unless $!done {
            .perform() for @!dependencies;
            &!callback();
            $!done = True;
        }
    }
}
 
my $eat =
    Task.new({ say 'eating dinner. NOM!' },
        Task.new({ say 'making dinner' },
            Task.new({ say 'buying food' },
                Task.new({ say 'making some money' }),
                Task.new({ say 'going to the store' })
            ),
            Task.new({ say 'cleaning kitchen' })
        )
    );
 
$eat.perform();
------------------------------

== 使用 `class`

`Perl6` 和其他多数语言一样，使用 `class` 关键字定义类。
代码块可能包含任意的代码或者其他的代码块，而类通常只会包含状态以及行为声明。
示例代码使用 `has` 定义了属性（状态），并通过 `method` 关键字定义了行为。

类的声明创建了一个新的类型，默认情况下只在当前包可见（如同一个使用 `our` 声明的变量）。
这个类型对象是这个类的“空实例”，比如，像 `Int` 和 `Str` 这样的类型是 `Perl6` 内置类之一的类型对象。
上面的例子中使用了类名字 `Task`，以便之后代码能通过它引用这个类，例如通过调用 `new`
方法创建新的实例。

你可以使用 `.DEFINE` 方法来区分你拥有的是一个实例还是一个类型对象：
[source,perl6]
---------------------------
say Int.DEFINITE; # OUTPUT: «False␤» (类型对象) 
say 426.DEFINITE; # OUTPUT: «True␤»  (类的实例) 
 
class Foo {};
say Foo.DEFINITE;     # OUTPUT: «False␤» (类型对象) 
say Foo.new.DEFINITE; # OUTPUT: «True␤»  (实例) 
---------------------------

== 状态

类中的前三行代码都是属性（在其他语言叫做__字段__或者__实例存储__）的声明。
就像 `my` 变量无法在超出它声明域以外访问一样，属性在类外无法访问，封装性是
面向对象设计的关键原则之一。

第一个声明指定了一个回调（`callback`） - 用来表示一段完成指定任务的代码的对象的实例存储：

`has &!callback;`

`Sigil` `&` 表明这个属性表示某个可执行的东西，`!` 是一个 __twigil__或者说 第二 `sigil`，
`twigil` 是变量名字的一部分。
当前情况下，`!` 强调这个属性对于类来说是私有的。

第二个声明依然使用了私有的 `twigil`：

`has Task @!dependencies;`

不过，这个属性表示项的数组，所以它使用了 `@` `sigil`。
这些项指定了在当前任务执行之前必须完成的任务，此外，属性的类型声明表明数组只可以存储 `Task` 类（或者它的子类）的实例。

第三个属性表示当前任务的完成状态：

`has Bool $.done;`

这个标量（使用 `$` `sigil`）属性的类型是 `Bool`，没有使用 `!` `twigil`，而是 `.`。
`Perl6` 在强制属性的封装时，同时允许你使用访问器方法，将 `!` 替换成 `.` 就会同时声明了属性
`$!done` 和名字为 `done` 的访问器方法。
如果你写了以下代码：
[source,perl6]
--------------------
has Bool $!done;
method done() { return $!done; }
--------------------
需要注意的是这和其它一些语言允许声明公共属性不同，不用手动定义方法，你真的同时拥有了私有属性和方法。
如果你需要更复杂的逻辑而不是直接返回值，你还定义自己的访问器方法。

NOTE: 访问器方法只有在没有同名方法的时候才会生成（`only generate when missing one`）。

注意使用 `.` `twigil` 只会创建支持对属性的只读访问的方法，如果对象的使用者可以重置任务的状态（可能是为了再执行一次），你可以改变你的属性声明：

`has Bool $.done is rw;`

`Trait` `is rw` 会使得生成的方法返回外部可以修改属性值的值。

你还可以为属性提供默认值（有无访问器不影响默认值）：

`has Bool $.done = False;`

这个赋值会在对象构建的时候执行，右边的值将会在那时候求值，并且可以引用之前声明的属性：
[source,perl6]
--------------------
has Task @!dependencies;
has $.ready = not @!dependencies;
--------------------

== 静态字段？

尽管 `Perl6` 没有 **static** 关键字，任何一个模块可以做的事情类都可以做，所以使用作用域变量看起来是一个好注意。

[source,perl6]
--------------------
class Singleton {
    my Singleton $instance;
    method new {!!!}
    submethod instance {
        $instance = Singleton.bless unless $instance;
        $instance;
    }
}
--------------------

使用 `my` 或者 `our` 定义类的属性也可以在声明时就初始化，不过在这里没实现的是单例模式，
这个对象必须在第一次使用时创建。
很难保证类属性会在什么时候被初始化，因为它可以在编译期、运行期或者同时跨两个时期，
尤其是在使用 `use` 导入类的时候（译：导入引用的类？）。

[source,perl6]
------------------
class HaveStaticAttr {
      my Foo $.foo = some_complicated_subroutine;
}
------------------

类属性也可以使用第二 `sigil` - 与对象属性相同的风格 - 如果属性是公共的将会自动生成一个只读的访问器。

== 方法

正如属性给予了对象状态，方法给予对象行为。
让我们先暂时忽略 `new` 方法，这是一个特别的方法。
思考第二个方法 `add-dependency`，它将新的 `task` 加入到依赖列表。

[source,perl6]
------------------
method add-dependency(Task $dependency) {
    push @!dependencies, $dependency;
}
------------------

这多多少少看起来像是一个 `sub` 声明，然而它们之间有两个重要的不同点。
首先，声明的 **例程** 作为方法会加入到当前类的方法列表中，这样任意一个 `Task` 的实例都可以使用方法调用运算符 `.` 调用该方法。
其次，方法调用会将调用者（`invocant`）放入特殊变量 `self`。

这个方法接受传递的参数 - 必须是类 `Task` 的实例 - 然后将它 **加入** 到调用者的 `@!dependencies` 属性中。

方法 `perform`  包含着依赖处理的主体逻辑：
[source,perl6]
------------------
method perform() {
    unless $!done {
        .perform() for @!dependencies;
        &!callback();
        $!done = True;
    }
}
------------------

它不接受参数，而是使用对象的属性。
首先，它会通过检测 `$!done` 的值确认当前任务是否已经完成，如果已完成，那就什么也不做。

否则，它就会使用 `for` 遍历属性 `@!dependenceies` 来执行所有依赖的任务。
迭代操作会将每一项 - 每一项都是 `Task` 对象 - 放入到主题变量（`topic variable`）`$_`，
使用方法调用运算符 `.` 省略调用者时将会使用当前的主题变量作为调用者。
这样，迭代操作对  `@!dependenceies` 中的每一个 `Task` 对象调用了 `.perform` 方法。

当所有依赖完成之后，是时候执行 `&!callback` 完成当前 `Task` 的任务了，这里使用圆括号来完成调用。
最后，将 `$!done` 的值设置为 `True`，这样之后的对当前对象 `perform` 的调用（比如当前 `Task` 是其他 `Task` 的一个依赖）就不会重复执行任务了。

== 私有方法

像属性一样，方法也可以是私有的。
使用 `!` 作为前缀标记声明一个私有方法，它们使用 `self!` 加上方法名字来调用。
一个类调用另一个类的私有方法叫做该类被被调用类信任（`trust`），信任关系可以用 `trust` 关键字来完成，
需要注意的是被信任的类必须已经声明。
调用其他类的私有方法需要它的一个实例以及完全限定（`fully qualified`）的方法名。
信任也会允许私有属性的访问。

[source,perl6]
----------------
class B {...}
 
class C {
    trusts B;
    has $!hidden = 'invisible';
    method !not-yours () { say 'hidden' }
    method yours-to-use () {
        say $!hidden;
        self!not-yours();
    }
}
 
class B {
    method i-am-trusted () {
        my C $c.=new;
        $c!C::not-yours();
    }
}
 
C.new.yours-to-use(); # 这个方法的上下文是全局（GLOBAL），不被 C 信任
B.new.i-am-trusted();
----------------

信任关系不可以被继承，要将全局名称空间加入信任列表，可以信任伪包（`pseudo package`）`GLOBAL`。

== 构造函数

在构造函数这方面，`Perl6` 相对其他语言相当自由，构造函数是任何返回当前类实例的任何东西。
此外，构造函数只是普通的方法，你拥有一个继承自 `Mu` 的默认构造函数 `new`，不过，如同示例一样，你可以重写它：

[source,perl6]
---------------
method new(&callback, *@dependencies) {
    return self.bless(:&callback, :@dependencies);
}
---------------

`Perl6` 的构造函数不同于其他语言，比如 `C#` `Java`，它们在一个已经被魔法般创建的对象上设置状态，`Perl6`
的构造函数自己创建对象。
最简单的途径就是调用 `bless` 方法，它也是从 `Mu` 中继承而来，`bless` 方法接受一系列的命名参数，
用于设置属性的初始值。

例子中的构造函数将位置参数转换为命名参数，这样类就可以向其用户提供一个良好的构造函数。
第一个参数是回调函数（用来执行任务的东西），剩余参数则都是依赖的 `Task` 实例，构造函数捕获所有的依赖提供给
`@dependenceies` 数组，之后作为命名参数传递给 `bless`（注意 `:&callback` 使用了变量名字 - 去掉 `sigil` - 作为参数的名字）。

私有属性在任何时候都是私有的，这意味着 `bless` 不会被允许直接绑定 `&!callback` 和 `@!dependenceies`。
要做到这个，我们需要重写 `BUILD` 子方法，它将会在创建对象时被 `bless` 调用：

`submethod BUILD(:&!callback, :@!dependencies) { }`

因为 `BUILD` 在当前新创建的对象上下文执行，它被允许手动操作这些私有属性。
这里的技巧是私有属性（`&!callback` 和 `@!dependenceies`）被用做 `BUILD` 的参数的绑定目标，
零样板初始化（`Zero-boilerplate initialization!`）。
更多信息请参考 https://docs.perl6.org/language/objects#Object_Construction[objects]。

`BUILD` 用来初始化所有的私有属性以及处理默认值：

[source,perl6]
---------------------
has &!callback;
has @!dependencies;
has Bool ($.done, $.ready);
submethod BUILD(
        :&!callback,
        :@!dependencies,
        :$!done = False,
        :$!ready = not @!dependencies
    ) { }
---------------------

关于对象构造函数以及属性初始化更多作用请参考 https://docs.perl6.org/language/objects#Object_Construction[Object Construction]。

== 使用类

在创建完类之后，你就可以创建类的实例了，自定义的构造函数提供了一种非常简单的方式声明任务和它的依赖。
要创建一个单独的没有依赖的任务：

[source,perl6]
------------------------
my $eat = Task.new({say 'eating dinner. NOW1' });
------------------------

前面的小节已经说明声明一个类 `Task` 将会把它的类型对象加入到当前的命名空间，这个类型对象是类的“空的实例”，更具体点就是没有任何状态的实例。
你可以调用它的方法，但是不要尝试访问任何的状态，`new` 就是一个例子，它创建一个新的对象而不是修改或者访问当前存在的对象。

不幸的是，天下不会掉馅饼，晚餐任务依赖其他任务：

[source,perl6]
--------------------------
my $eat =
    Task.new({ say 'eating dinner. NOM!' },
        Task.new({ say 'making dinner' },
            Task.new({ say 'buying food' },
                Task.new({ say 'making some money' }),
                Task.new({ say 'going to the store' })
            ),
            Task.new({ say 'cleaning kitchen' })
        )
    );
--------------------------

注意自定义的构造函数和合理的空白是如何让依赖清晰起来的。

最后，`perform` 的调用会递归的顺序调用其他依赖的 `perform` 方法，输出以下内容：

    making some money
    going to the store
    buying food
    cleaning kitchen
    making dinner
    eating dinner. NOM!

== 继承

面向对象提供了继承的概念来作为代码重用的机制之一，`Perl6` 支持一个类继承自另一个或者多个类。
当一个类继承自另一个类的时候，它会通知方法调度器（`method dispatcher`）沿着继承链寻找方法，这种情况适用于
通过 `method` 关键字声明的方法以及形如属性访问器的自动生成方法。

[source,perl6]
---------------------------
class Employee {
    has $.salary;
 
    method pay() {
        say "Here is \$$.salary";
    }
}
 
class Programmer is Employee {
    has @.known_languages is rw;
    has $.favorite_editor;
 
    method code_to_solve( $problem ) {
        say "Solving $problem using $.favorite_editor in "
        ~ $.known_languages[0] ~ '.';
    }
}
---------------------------

现在，`Programmer` 的任何对象都可以访问定义在 `Employee` 内方法和访问器，尽管它们由 `Programmer` 类生成：

[source,perl6]
--------------------------
my $programmer = Programmer.new(
    salary => 100_000,
    known_languages => <Perl5 Perl6 Erlang C++>,
    favorite_editor => 'vim'
);
 
$programmer.code_to_solve('halting problem');
$programmer.pay();
--------------------------

=== 重写继承的方法

无疑，类可以通过自定义重写继承自的父类的方法和属性，下面的例子示范了 `Baker` 如何重写继承自 `Cook` 的方法：

[source,perl6]
---------------------------
class Cook is Employee {
    has @.utensils  is rw;
    has @.cookbooks is rw;
 
    method cook( $food ) {
        say "Cooking $food";
    }
 
    method clean_utensils {
        say "Cleaning $_" for @.utensils;
    }
}
 
class Baker is Cook {
    method cook( $confection ) {
        say "Baking a tasty $confection";
    }
}
 
my $cook = Cook.new(
    utensils => <spoon ladle knife pan>,
    cookbooks => 'The Joy of Cooking',
    salary => 40000);
 
$cook.cook( 'pizza' ); # 输出： «Cooking pizza␤» 
 
my $baker = Baker.new(
    utensils => 'self cleaning oven',
    cookbooks => "The Baker's Apprentice",
    salary => 50000);
 
$baker.cook('brioche'); # 输出： «Baking a tasty brioche␤» 
---------------------------

因为调度器会在它移动到父类之前看到 `Baker` 中的 `cook` 方法，`Baker` 中的 `cook` 方法将会被调用。

要访问继承链中的方法，请使用 https://docs.perl6.org/language/functions#Re-dispatching[re-disptach] 或者     https://docs.perl6.org/type/Metamodel::ClassHOW#method_can[MOP] 。

=== 多继承

正如之前提到的那样，一个类可以继承自多个父类。
当使用多继承时，调度器查找方法时会查找所有的父类。
`Perl6` 使用 **C3** 算法将多继承层次结构线性化， 这是对 `Perl5` 's 的处理多继承的默认方法（深度优先搜索）的重大改进。

[source,perl6]
-------------------
class GeekCook is Programmer is Cook {
    method new( *%params ) {
        push( %params<cookbooks>, "Cooking for Geeks" );
        return self.bless(|%params);
    }
}
 
my $geek = GeekCook.new(
    books           => 'Learning Perl 6',
    utensils        => ('stainless steel pot', 'knife', 'calibrated oven'),
    favorite_editor => 'MacVim',
    known_languages => <Perl6>
);
 
$geek.cook('pizza');
$geek.code_to_solve('P =? NP');
-------------------

现在，对 `Programmer` 以及 `Cook` 可用的所有方法也对 `GeekCook` 可用。

虽然多继承是一个有用的概念，偶尔会用到，但是更重要是知道有更多的面向对象的概念。
当要使用多继承的时候，考虑设计是否可以使用 `roles` 来实现更好的设计是一个良好的习惯，这通常更加安全，
因为它们强制类的作者显式的解决方法名字冲突。更多信息请参考
https://docs.perl6.org/language/objects#Roles[Roles]

=== `also` 声明符

类的继承列表可以通过 `also` 并使用 `trait` `is` 写在类的内部，这同样适用于用于角色组合（`role composition`）的 `trait` `does`：

[source,perl6]
--------------------------
class GeekCook {
    also is Programmer;
    also is Cook;
    # ... 
}
 
role A {};
role B {};
class C { also does A; also does B }
--------------------------

== 内省

内省是在你的程序中收集对象信息的过程，不是通过阅读源码，而是通过查询对象某些属性，比如它的类型。

给予我们一个对象 `$o` 以及之前小节定义的类，我们可以问它几个问题：

[source,perl6]
--------------------------
if $o ~~ Employee { say "It's an employee" };
if $o ~~ GeekCook { say "It's a geeky cook" };
say $o.WHAT;
say $o.perl;
say $o.^methods(:local)».name.join(', ');
say $o.^name;
--------------------------

这将会输出：

    It's an employee
    (Programmer)
    Programmer.new(known_languages => ["Perl", "Python", "Pascal"],
            favorite_editor => "gvim", salary => "too small")
    code_to_solve, known_languages, favorite_editor
    Programmer
    
前两个测试使用智能匹配（`smart-match`）匹配类名，如果对象属于该类或者该类的子类，将会返回 `True`。
所以问题中的 `$o` 属于类 `Employee` 或者其子类，但不属于 `GeekCook`。

`.WHAT` 方法返回和当前对象 `$o` 关联的类型对象，这将会告诉我们它的真实类型，在这里是： `Programmer`。

`$o.perl` 返回一串可以执行的 `Perl` 代码，这可以重新生成原始对象 `$o`，
虽然并不是所有的情况下都能很好的工作，但在调试简单对象时候很有用。
footnote:[比如闭包就不能简单的通过这种方式重新生成，如果你不知道闭包也没关系。还有目前的实现也无法很好的处理循环的数据结构，但有些东西 `.perl` 处理的很好];

`$o.^methods(:local)` 将会产生可以在 `$o` 上调用的 https://docs.perl6.org/type/Method[方法]，
命名参数 `:local` 将返回的方法限制为在 `Programmer` 类内定义的，排除了继承的方法。

使用 `.^` 代替 `.` 调用方法的语法意味着方法的调用将会作用于元类，它是一个管理 `Programmer` 或者其他你感兴趣的类 的属性的类。元类还有其他内省的方式：

[source,perl6]
--------------------------
say $o.^attributes.join(', ');
say $o.^parents.map({ $_.^name }).join(', ');
--------------------------

最终，`$o.^name` 在元对象上调用 `name` 方法，毫不意外的返回类的名字。

内省对于调试和学习新的语言或者库非常有用，当一个函数或者方法返回你不知道的类型时，
使用 `.WHAT` 查询它的类型；使用 `.perl` 查看构造的方法等等，这样你将会对于返回的数据
有一个很好的认识。使用 `.^methods` 你可以知道可以对这个类做什么。

不过它们还有其他的功能：一个可以将对象序列化的例程比如知道类的属性，这也可以通过内省知道。
